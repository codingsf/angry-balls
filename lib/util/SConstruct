AddOption('--debug-build',
          default = False,
          action = 'store_true',
          help = 'Build in debug mode.'
         )
AddOption('--use-asan',
          default = False,
          action = 'store_true',
          help = 'Use AddressSanitizer.'
         )
debug_mode = GetOption('debug_build')
use_asan = GetOption('use_asan')

import os

project_root = os.path.abspath('../../')
build_subdir = 'debug' if debug_mode else 'release'


cpp_path = ['include'
           ]
cpp_path = [os.path.join(project_root, x) for x in cpp_path]


cpp_flags = ['-Wall',
             '-std=c++11',
             '-stdlib=libc++',
             '-pthread'
            ]
if debug_mode:
    cpp_flags.append(['-g'])
else:
    cpp_flags.append(['-O2'])

if use_asan:
    cpp_flags.append(['-fno-omit-frame-pointer',
                      '-fno-optimize-sibling-calls',
                      '-fsanitize=address'
                     ]
                    )
compiler = 'clang++'


env = Environment(CXX = compiler,
                  CPPFLAGS = cpp_flags,
                  CPPPATH = cpp_path
                 )

SConscript('SConscript',
           variant_dir = build_subdir,
           exports=['env']
          )
